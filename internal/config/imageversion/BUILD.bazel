# gazelle:ignore

load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("//bazel/oci:containers.bzl", "config_containers")
load("//bazel/oci:pin.bzl", "oci_go_source")

GENERATED_SRCS = [
    ":" + container["name"]
    for container in config_containers()
]

go_library(
    name = "imageversion",
    srcs = ["imageversion.go"] + GENERATED_SRCS,  # keep
    importpath = "github.com/edgelesssys/constellation/v2/internal/config/imageversion",
    visibility = ["//:__subpackages__"],
    deps = ["//internal/containerimage"],
)

[
    oci_go_source(
        name = container["name"],
        identifier = container["identifier"],
        image_name = container["image_name"],
        oci = container["oci"],
        package = "imageversion",
        repotag_file = container["repotag_file"],
        visibility = ["//:__subpackages__"],
    )
    for container in config_containers()
]
